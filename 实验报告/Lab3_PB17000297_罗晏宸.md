# <center>实验报告</center>
### <center>实验三	寄存器堆与计数器</center>
##### <p align="right">罗晏宸</br>PB17000297</br>2019.4.11</p>
***
## 实验目的
1. **复习寄存器类型**  
    &emsp;&emsp;复习Verilog中`reg`类型的特性与使用方法，包括寄存器数组的定义与使用  
2. **复习控制七段数码管显示输出**  
    &emsp;&emsp;复习通过时钟分频的方式控制多个七段数码管的显示，包括计数器的编写与时钟IP核的例化等等  
3. **实现寄存器堆**  
    &emsp;&emsp;实现拥有两个异步读端口和一个同步写端口的寄存器堆(Register File)，数据位宽与寄存器数由参数控制，以实现存储数据的功能  
4. **实现计数器**  
    &emsp;&emsp;实现拥有同步置数，异步清零功能的计数器  
5. **实现循环队列**  
    &emsp;&emsp;通过例化寄存器堆实现最大长度为8的FIFO循环队列，并能够在七段数码管上显示队列数据状态以及出入队信息
***
## 实验内容
### 1. 寄存器堆
- **程序代码 <font face="consolas">RegisterFile.v</font>**
    ```verilog {.line-numbers}
    module RegisterFile #(parameter M = 3, N = 4)(Read_Address_0, Read_Address_1, Write_Address, Write_Data, Write_Enable, CLK100MHZ, reset, Read_Data_0, Read_Data_1);
        input CLK100MHZ, reset;
        input Write_Enable;
        input [M - 1 : 0] Read_Address_0, Read_Address_1;
        input [M - 1 : 0] Write_Address;
        input [N - 1 : 0] Write_Data;
        output [N - 1 : 0] Read_Data_0, Read_Data_1;
        
        reg [N - 1 : 0] Register [1 : (1 << M) - 1];
        integer i;
        
        assign Read_Data_0 = (Read_Address_0 == 0) ? 0 : Register[Read_Address_0];
        assign Read_Data_1 = (Read_Address_1 == 0) ? 0 : Register[Read_Address_1];
        
        always  @(posedge CLK100MHZ or posedge reset)
        begin
            if (reset)
            begin
                for (i = 1; i < N; i = i + 1)
                    Register[i] <= 0;
            end
            else
            begin
                if ((Write_Address != 0) && Write_Enable)
                    Register[Write_Address] <= Write_Data;
            end
        end
    endmodule
    ```
- **原理逻辑电路图**
    ![寄存器堆原理](https://github.com/lyc0930/COD/blob/master/RegisterFile/RegisterFile_RTL.png?raw=true)
    **逻辑设计说明：**
    >&emsp;&emsp;&emsp;&emsp;本工程实现了可参数化的寄存器堆，参数`M`表示寄存器堆大小为$2^M$（其中地址为`0`的寄存器缺省不可用），`N`表示数据位宽。`Write_Enable`为使能信号，在其高电平下，输入时钟信号上升沿时实现向`Write_Address`标志的寄存器堆地址中写入内容为`Write_Data`的数据；`Read_Address_0, Read_Address_1`标志了异步读取寄存器堆数据的两个地址，相应地址中的数据输出分别为`Read_Data_0, Read_Data_1`；`reset`是异步复位信号，复位将寄存器堆各地址中的数据重置为`'b0`
- **仿真代码 <font face="consolas">RegisterFile_tb.v</font>**
    ```verilog {.line-numbers}
    module RegisterFile_tb();
        reg Write_Enable;
        reg [5 : 0] Read_Address_0, Read_Address_1;
        reg [5 : 0] Write_Address;
        reg [31 : 0] Write_Data;
        wire [31 : 0] Read_Data_0, Read_Data_1;
        reg reset;
        wire clock;
        GenerateClock CLK (clock);
        RegisterFile DUT (.Read_Address_0(Read_Address_0), .Read_Address_1(Read_Address_1), .Write_Address(Write_Address), .Write_Data(Write_Data), .Write_Enable(Write_Enable), .clock(clock), .reset(reset), .Read_Data_0(Read_Data_0), .Read_Data_1(Read_Data_1));
        
        initial
        begin
            reset = 'b1;#10;
            reset = 'b0;
            Read_Address_0 = 'd3;
            Read_Address_1 = 'd5;
            
            Write_Data = 32'b11000000;
            Write_Address = 'd3;
            Write_Enable = 'b1;#10;
            Write_Enable = 'b0;
            #10;
            Write_Data = 32'b1100111000;
            Write_Address = 'd5;
            Write_Enable = 'b1;#10;
            Write_Enable = 'b0;
            #10;
            Write_Data = 32'b11;
            Write_Address = 'd3;
            Write_Enable = 'b1;#10;
            Write_Enable = 'b0;
            
            reset = 'b1;#10;
            reset = 'b0;
            Read_Address_0 = 'd2;
            Read_Address_1 = 'd3;
            
            Write_Data = 32'b01;
            Write_Address = 'd3;
            Write_Enable = 'b1;#10;
            Write_Enable = 'b0;
        end
        
    endmodule
    ```
    ```verilog {.line-numbers}
    module GenerateClock(output reg Clk);
        parameter tON = 10,tOFF = 10;
        
        initial
        begin
                Clk = 0;
                forever
                begin
                    #tOFF Clk = 1;
                    #tON Clk = 0;
                end
        end
    endmodule
    ```
- **仿真截图**
    ![寄存器堆仿真截图](https://github.com/lyc0930/COD/blob/master/RegisterFile/RegisterFile_simulation.png?raw=true)
    **仿真截图说明：**
    >&emsp;&emsp;&emsp;&emsp;以周期为20ns的时钟信号进行时长80ns的仿真：70ns之前，`Read_Address_0`与`Read_Address_1`分别为`'d3`与`'d5`，对于寄存器堆内这两个地址内的数据进行持续观察：第一次`reset`复位信号脉冲之后，寄存器堆各地址数据重置为`'d0`，之后通过改变`Write_Address`与`Write_Data`分别向地址为`'d3`与`'d5`的寄存器中写入数据`'d192`与`'d824`，通过`Read_Data_0`与`Read_Data_1`观察到相应正确的结果
- **下载照片**
    ![排序下载照片1](https://github.com/lyc0930/COD/blob/master/RegisterFile/IMG_20190410_232905.jpg?raw=true)
    ![排序下载照片2](https://github.com/lyc0930/COD/blob/master/RegisterFile/IMG_20190410_232937.jpg?raw=true)
    **下载照片说明：**
    >&emsp;&emsp;&emsp;&emsp;在默认参数`M = 3, N = 4`下综合实现，使用板载的100MHZ时钟，按动按钮`BTNU`输入一个复位`reset`电平脉冲后，将`swt[15 : 12]`拨成`4'b1011`，`swt[10 : 8]`拨成`3'b111`，表示输入`Write_Data = 4'b1011, Writa_Address = 3'111`，按动按钮`BTNC`将`Write_Enable`信号设为高电平，再将`swt[15 : 12]`拨成`4'b1001`，`swt[10 : 8]`拨成`3'b101`，表示输入`Write_Data = 4'b1001, Writa_Address = 3'101`，按动按钮`BTNC`将`Write_Enable`信号设为高电平，将`swt[6 : 4]`拨成`3'b111`，`swt[2 : 1]`拨成`3'b101`，表示输入`Read_Address_0 = 3'b101, Read_Address_1 = 3'b111`，此时相应的`led[7], led[5 ： 3], led[0]`亮起，如图一所示，表示输出`Read_Data_0 = 4'b1001, Read_Data_1 = 4'b1011`  
    >&emsp;&emsp;&emsp;&emsp;随后再次按动按钮`BTNU`输入一个复位`reset`电平脉冲，将`swt[15 : 12]`拨成`4'b0001`，`swt[10 : 8]`拨成`3'b001`，表示输入`Write_Data = 4'b0001, Writa_Address = 3'001`，按动按钮`BTNC`将`Write_Enable`信号设为高电平，将`swt[2 : 0]`拨成`3'b001`，表示输入`Read_Address_0 = 3'b001`，此时相应的`led[0]`亮起，其余LED灯均灭，如图一所示，表示输出`Read_Data_0 = 4'b0001, Read_Data_1 = 4'b0000`
</br>
### 2. 计数器
- **程序代码 <font face="concolas">Counter.v</font>**
    ```verilog {.line-numbers}
    module Counter #(parameter N = 4)(Data, Set_Enable, Count_Enable, reset, CLK100MHZ, Q);
        input [N - 1 : 0] Data;
        input Set_Enable, Count_Enable;
        input reset, CLK100MHZ;
        output [N - 1 : 0] Q;
        
        reg [N - 1 : 0] Count;
        wire CLK5MHZ, clock;
        
        clk_wiz_0 CLK100M_to_5M (CLK5MHZ, CLK100MHZ);
        FrequencyDivision CLK5M_to_1k (CLK5MHZ, 0, clock);
        assign Q = Count;
        
        always @(posedge clock or posedge reset)
        begin
            if (reset)
                Count <= 0;
            else
            begin
                if (Set_Enable)
                    Count <= Data; 
                else if (Count_Enable)
                    Count <= Count + 1;
            end
        end
    endmodule
    ```
    ```verilog {.line-numbers}
    module FrequencyDivision(input Origin_Clock, input reset, output pulse);
        reg [23:0] count;
        always @(posedge Origin_Clock or posedge reset)
        begin
            if (reset)
                count <= 'd0;  		
            else 
            begin
                if(count >= 'd4999999)
                    count <= 'd0;
                else
                    count <= count + 'd1;
            end
        end
        assign	pulse =  (count == 'd2999999) ? 1'b1 : 1'b0;
    endmodule
    ```
- **原理逻辑电路图**
    ![计数器原理](https://github.com/lyc0930/COD/blob/master/Counter/Counter_RTL.png?raw=true)
    **逻辑设计说明：**
    >&emsp;&emsp;&emsp;&emsp;通过例化`Clock Wizard`IP核以及实例化时钟分频模块`FrequencyDivision`实现了一个有同步置数（`Set_Enable`为置数使能信号，`Data`为置数输入）和异步清零（`reset`为复位信号）功能的计数器，时钟控制为1s，计数上限$2^N$由参数`N`设定
- **仿真代码 <font face="consolas">Counter_tb.v</font>**
    ```verilog {.line-numbers}
    module Counter_tb();
        reg [3 : 0] Data;
        reg Set_Enable, Count_Enable;
        reg reset;
        wire clock;
        wire [3 : 0] Q;
        
        GenerateClock CLK (clock);
        Counter DUT (.Data(Data), .Set_Enable(Set_Enable), .Count_Enable(Count_Enable), .reset(reset), .clock(clock), .Q(Q));
        
        initial
        begin
            Data = 4'b0110;
            reset = 'b1;#10;
            reset = 'b0;#10;
            Count_Enable = 'b1;#70;
            Set_Enable = 'b1;#10;
            Count_Enable = 'b0;#20;
            reset = 'b1;#10;
            Set_Enable ='b0;
            Count_Enable = 'b1;#20;
            reset = 'b0;
            Count_Enable = 'b1;#60;
            Count_Enable = 'b0;
        end
    endmodule
    ```
- **仿真截图**
    ![计数器仿真截图](https://github.com/lyc0930/COD/blob/master/Counter/Counter_simulation.png?raw=true)
    **仿真截图说明：**
    >&emsp;&emsp;&emsp;&emsp;在默认参数下对模块进行时长为240ns的仿真：在一个`reset`复位脉冲信号之后，在20~90ns，`Count_Enable`为高电平，`Set_Enable`为低电平，计数器保持计数，`Q`从`'d0`逐一增加到`'d3`；随后`Set_Enable`为高电平，将`Q`置为与`Data`相同的值`'d6`；再随后一个`reset`复位脉冲后，`Q`重置为`0`，在高电平`Count_Enable`下继续计数
- **下载照片**
    ![计数器下载照片1](https://github.com/lyc0930/COD/blob/master/Counter/IMG_20190410_222202.jpg?raw=true)
    ![计数器下载照片2](https://github.com/lyc0930/COD/blob/master/Counter/IMG_20190410_222226.jpg?raw=true)
    **下载照片说明：**
    >&emsp;&emsp;&emsp;&emsp;将`swt[3], swt[0]`拨至开，表示输入为`Data = 4'b1001`，拨动`swt[14]`将置数信号`Set_Enable`设为高电平，在一次时钟上升沿后，`led[3]`与`led[0]`亮起，表示计数器的值被设置为`4'b1001`，如图一所示；随后将`swt[14]`拨至关，将`swt[15]`拨至开，表示计数使能`Count_Enable`为高电平，计数器正常计数4s后，`led[3 : 0]`与`led[0]`亮起，表示计数器的值为`4'b1001 + 'd4 = 4'b1101`
</br>
### 2. FIFO循环队列
- **程序代码 <font face="concolas">Queue.v</font>**
    ```verilog {.line-numbers}
    module Queue #(parameter M = 8, N = 4)(en_out, en_in, in, CLK100MHZ, reset, Display, out, empty, full);
        input en_out, en_in;
        input CLK100MHZ, reset;
        input [N - 1 : 0] in;
        output reg [N - 1 : 0] out;
        output empty, full;
        output [15 : 0] Display;//[15 : 8] an [7] dp [6 : 0] seg  
        
        wire CLK5MHZ, clock;
        wire [2 : 0] pulse;
        wire [N - 1 : 0] EnQueue_Data, DeQueue_Data, Queue_Data;
        wire Enable_in, Enable_out;
        wire [(M >> 1) - 1 : 0] EnQueue_Address;//1 : 8
        reg [(M >> 1) - 2 : 0] head, rear;
        reg tag;
        
        // use 4'b1111 as nothing
        clk_wiz_0 CLK100MHZ_to_5MHZ (CLK5MHZ, CLK100MHZ);
        FrequencyDivision CLK5MHZ_to_1sPulse (CLK5MHZ, 'b0, clock);
        SegmentPulse CLK100MHZ_to_SegmentChoose (CLK100MHZ, 'b0, pulse);
        RegisterFile #((M >> 1), 4) QUEUE (head + 'b1, pulse + 'b1, EnQueue_Address, EnQueue_Data, Enable_in ^ Enable_out, clock, ~reset, DeQueue_Data, Queue_Data);
        BCD_to_7Segment ENCODE (Queue_Data, Display[6 : 0]);
        
        assign empty = (head == rear) && (tag == 'b0);
        assign full = (head == rear) && (tag == 'b1);
        assign Enable_in = en_in && ~full;
        assign Enable_out = en_out && ~empty;
        assign EnQueue_Address = ((Enable_in) ? rear : head) + 'b1;
        assign EnQueue_Data = (Enable_in) ? in : 4'b1111;
        assign Display[7] = (pulse == head) ? 0 : 1; 
        assign Display[8] = (pulse == 'd7) ? 0 : 1;
        assign Display[9] = (pulse == 'd6) ? 0 : 1;
        assign Display[10] = (pulse == 'd5) ? 0 : 1;
        assign Display[11] = (pulse == 'd4) ? 0 : 1;
        assign Display[12] = (pulse == 'd3) ? 0 : 1;
        assign Display[13] = (pulse == 'd2) ? 0 : 1;
        assign Display[14] = (pulse == 'd1) ? 0 : 1;
        assign Display[15] = (pulse == 'd0) ? 0 : 1;
        
        always @(posedge clock or posedge reset)
        begin
            if (reset)
            begin
                tag <= 'b0;
                out <= 4'b0000;
                head <= 'b0;
                rear <= 'b0;
            end
            else if (Enable_in ^ Enable_out)
            begin
                if (Enable_in)
                begin
                    rear <= rear + 'b1;
                    tag <= 'b1;
                end
                else if (Enable_out)
                begin
                    out <= DeQueue_Data;
                    head <= head + 'b1;
                    tag <= 'b0;
                end
            end
        end
    endmodule
    ```
    ```verilog {.line-numbers}
    module FrequencyDivision(input Origin_Clock, input reset, output pulse);
        reg [23:0] count;
        always @(posedge Origin_Clock or posedge reset)
        begin
            if (reset)
                count <= 'd0;  		
            else 
            begin
                if(count >= 'd9999999)
                    count	<= 'd0;
                else
                    count	<= count + 'd1;
            end
        end
        assign	pulse =  (count == 'd4999999) ? 1'b1 : 1'b0;
    endmodule
    ```
    ```verilog {.line-numbers}
    module SegmentPulse(input Origin_Clock, input reset, output [2 : 0] pulse);
        reg [17:0] count;
        always @(posedge Origin_Clock or posedge reset)
        begin
            if (reset)
                count <= 'd0;  		
            else 
            begin
                if(count >= 'd117096)
                    count	<= 'd0;
                else
                    count	<= count + 'd1;
            end
        end
        assign	pulse = count[16 : 14];
    endmodule
    ```
    ```verilog {.line-numbers}
    module BCD_to_7Segment(
    input [3:0] x,
    output [6:0] seg
    );
        assign seg[0] = (x == 4'b1111) || (((~x[3] && ~x[2] && ~x[1] && x[0]) | (~x[3] && x[2] && ~x[1] && ~x[0])) && ~(x[3] && (x[2] | x[1])));
        assign seg[1] = (x == 4'b1111) || ((~x[3] && x[2] && ~x[1] && x[0]) | (~x[3] && x[2] && x[1] && ~x[0]) | (x[3] && (x[2] | x[1])));
        assign seg[2] = (x == 4'b1111) || ((~x[3] && ~x[2] && x[1] && ~x[0]) | (x[3] && (x[2] | x[1])));
        assign seg[3] = (x == 4'b1111) || (((~x[3] && ~x[2] && ~x[1] && x[0]) | (~x[3] && x[2] && ~x[1] && ~x[0]) | (~x[3] && x[2] && x[1] && x[0])) && ~(x[3] && (x[2] | x[1])));
        assign seg[4] = (x == 4'b1111) || (((~x[3] && ~x[2] && ~x[1] && x[0]) | (~x[3] && ~x[2] && x[1] && x[0]) | (~x[3] && x[2] && ~x[1] && ~x[0]) | (~x[3] && x[2] && ~x[1] && x[0]) | (~x[3] && x[2] && x[1] && x[0]) | (x[3] && ~x[2] && ~x[1] && x[0])) && ~(x[3] && (x[2] | x[1])));
        assign seg[5] = (x == 4'b1111) || (((~x[3] && ~x[2] && ~x[1] && x[0]) | (~x[3] && ~x[2] && x[1] && ~x[0]) | (~x[3] && ~x[2] && x[1] && x[0]) | (~x[3] && x[2] && x[1] && x[0])) && ~(x[3] && (x[2] | x[1])));
        assign seg[6] = (x == 4'b1111) || (((~x[3] && ~x[2] && ~x[1] && ~x[0]) | (~x[3] && ~x[2] && ~x[1] && x[0]) | (~x[3] && x[2] && x[1] && x[0])) && ~(x[3] && (x[2] | x[1])));    
    endmodule
    ```
- **原理逻辑电路图**
    ![队列原理](https://github.com/lyc0930/COD/blob/master/Queue/Queue_RTL.png?raw=true)
    **逻辑设计说明：**
    >&emsp;&emsp;&emsp;&emsp;通过例化寄存器堆实现由`en_in`与`en_out`控制入队与出队的8位循环队列，入队数据由`in[4]`输入，出队数据输出到`out[4]`，由`empty`与`full`信号标志队列空与满的状态，复位信号`reset`将队列清空，队列的当前元素由`Display[16]`输出到七段数码管上
- **仿真代码 <font face="consolas">Queue_tb.v</font>**
    ```verilog {.line-numbers}
    module Queue_tb();
        wire CLK100MHZ;
        reg en_out, en_in, reset;
        reg [3 : 0] in;
        wire empty, full, tag;
        wire [3 : 0] out;
        wire [3 : 0] head, rear;
        GenerateClock CLK (CLK100MHZ);
        Queue DUT (.en_out(en_out), .en_in(en_in), .in(in), .CLK100MHZ(CLK100MHZ), .reset(reset), .Display(), .out(out), .empty(empty), .full(full), .head(head), .rear(rear), .tag(tag));

        initial
        begin
            en_in = 'b0;
            en_out = 'b0;
            in = 'd0;
            reset = 'b1;#23;
            reset = 'b0;
            en_in = 'b1;
            in = 'd1;#23;
            in = 'd2;#23;
            in = 'd3;#23;
            in = 'd4;#23;
            in = 'd5;#23;
            in = 'd6;#23;
            in = 'd7;#23;
            en_in = 'b0;#33;
            en_out = 'b1;
        end
    endmodule
    ```
- **仿真截图**
    ![队列仿真截图](https://github.com/lyc0930/COD/blob/master/Queue/Queue_simulation.png?raw=true)
    **仿真截图说明：**
    >&emsp;&emsp;&emsp;&emsp;在默认参数下对模块进行时长为400ns的仿真：在一个`reset`复位脉冲信号之后，依次入队`'d1, 'd2, 'd3, 'd3, 'd4, 'd5, 'd6, 'd7`八个数据，直到队满标志`full`为高电平，再依次出队直到队空；仿真中忽略了数码管显示的输出，并增加了队首`head`与队尾`rear`元素的观察
- **下载照片**
    ![队列下载照片1](https://github.com/lyc0930/COD/blob/master/Queue/IMG_20190418_175203.jpg?raw=true)  
    ![队列下载照片2](https://github.com/lyc0930/COD/blob/master/Queue/IMG_20190418_175246.jpg?raw=true)  
    ![队列下载照片3](https://github.com/lyc0930/COD/blob/master/Queue/IMG_20190418_175303.jpg?raw=true)  
    ![队列下载照片4](https://github.com/lyc0930/COD/blob/master/Queue/IMG_20190418_175326.jpg?raw=true)  
    **下载照片说明：**
    >&emsp;&emsp;&emsp;&emsp;按动按钮`BTNC`输入一个`reset`复位信号脉冲之后，将`swt[15]`拨至开，表示数据入队，并依次拨动`swt[3 : 0]`将入队数据`in[4]`设为`4'b0111, 4'b0101, 4'b0001`，并控制其分别的停顿时间，在6个周期为2s的时钟上升沿后，队列状态如图一中七段数码管所示，其中首个`7`下方的小数点表示其为队首数据；拨动`swt[3 : 0]`将入队数据`in[4]`设为`4'b0001, 4'b0000`，在2个时钟上升沿后，队列状态如图二中七段数码管所示，此时`led17_R`亮起，表明队列已满  
    >&emsp;&emsp;&emsp;&emsp;将`swt[15]`拨至关，`swt[14]`拨至开，表示数据出队，在3个时钟上升沿后，队列状态如图三中七段数码管所示，其中首个`5`下方的小数点表示其为队首数据，`led[2]`与`led[0]`亮起，表示上一个出队数据为`4'b0101`；继续数据出队，在5个时钟上升沿后，队列状态如图四中七段数码管所示，此时`led16_B`亮起，表明队列已空
</br> 
## 实验总结
<p>&emsp;&emsp;本次实验重点是学习寄存器堆的结构原理、功能特性，并加以实现与应用，寄存器堆作为CPU的重要组成部分，通过信号控制数据存取是其核心功能，在此基础上，通过一个具体实例————8位FIFO循环队列的练习实现，对于此前数字电路实验中所学的七段数码管显示，包括信号控制与脉冲分频等知识做了有效的复习，并在此工程中成功实例化架构的寄存器堆模块，对其功能有了更为深入的理解，也从另一个角度对于经典数据结构及其算法在硬件上的实现有了全新的尝试与体会。</p>  

## 附
&emsp;&emsp;本报告中出现的所有设计与测试源代码文件以及相关截图与照片可见  
&emsp;&emsp;[GitHub@lyc0930](https://github.com/lyc0930/COD)