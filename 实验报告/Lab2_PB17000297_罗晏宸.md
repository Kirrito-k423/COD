# <center>实验报告</center>
### <center>实验二	数据通路与状态机</center>
##### <p align="right">罗晏宸</br>PB17000297</br>2019.3.28</p>
***
## 实验目的
1. **复习有限状态机(FSM)相关知识**  
    &emsp;&emsp;&emsp;&emsp;复习此前所学的状态机相关知识，熟悉二段式/三段式状态机的设计，以排序电路与除法器为例练习状态图的设计与实现
2. **掌握数据通路与状态机的设计方法**  
    &emsp;&emsp;&emsp;&emsp;学习在相对复杂的功能电路中控制单元与数据通路的连接方式，掌握例化此前设计的电路的具体方式
3. **实现多数排序**  
    &emsp;&emsp;&emsp;&emsp;功能上完成输入多个（4个）二进制无符号整数的排序与输出，电路基于状态机实现，由时钟与复位信号控制
4. **实现整数除法器**  
    &emsp;&emsp;&emsp;&emsp;实现两个二进制无符号整数的整除，电路基于状态机实现，由时钟与复位信号控制，在一定时钟周期后输出商与余数，并能对除以零等非法操作输出错误信号
***
## 实验内容
### 1. 排序
- **程序代码 <font face="consolas">Sort.v</font>**
    ```verilog {.line-numbers}
    module Sort(x0, x1, x2, x3, reset, clock, s0, s1, s2, s3, done);
        parameter DIGIT = 4;
        input wire [DIGIT - 1 : 0] x0, x1, x2, x3;
        input wire reset, clock;
        output reg [DIGIT - 1 : 0] s0, s1, s2, s3;
        output reg done;
        reg [2 : 0] currentstate, nextstate;

        parameter State_Disorder = 3'b001, 
                State_01Inorder = 3'b010,
                State_012Inorder = 3'b100;
        
        always @(posedge clock or posedge reset)
        begin
            if (reset)
                currentstate <= State_Disorder;
            else
                currentstate <= nextstate;
        end
        
        always @(*)
        begin
            case (currentstate)
                State_Disorder:
                begin
                    if (s0 > s1)
                        nextstate = State_01Inorder;
                    else
                        nextstate = State_01Inorder;
                end
                
                State_01Inorder:
                begin
                    if (s1 > s2)
                        nextstate = State_Disorder;
                    else
                        nextstate = State_012Inorder;
                end
                
                State_012Inorder:
                begin
                    if (s2 > s3)
                        nextstate = State_Disorder;
                    else
                        nextstate = State_012Inorder;
                end
                
                default :
                    nextstate = State_Disorder;
            endcase 
        end
        
        always @(posedge clock or posedge reset)
        begin
            if (reset) 
            begin
                s0 <= x0;
                s1 <= x1;
                s2 <= x2;
                s3 <= x3;
                done <= 'b0;
            end
            else
            begin
                done <= 'b0;
                case (currentstate)
                    State_Disorder:
                    begin
                        if (s0 > s1)
                        begin
                            s0 <= s1;
                            s1 <= s0;
                        end
                    end
                    
                    State_01Inorder:
                    begin
                        if (s1 > s2)
                        begin
                            s1 <= s2;
                            s2 <= s1;
                        end
                    end
                    
                    State_012Inorder:
                    begin
                        if (s2 > s3)
                        begin
                            s2 <= s3;
                            s3 <= s2;
                        end
                        else
                        done <= 'b1;
                    end

                    default :
                        done <= 'b0;
                endcase 
            end			
        end

    endmodule
    ```
- **原理逻辑电路图**
    ![排序逻辑](https://git.lug.ustc.edu.cn/luoyanchen/cod/raw/master/Sort/%E6%8E%92%E5%BA%8F%E9%80%BB%E8%BE%91%E5%9B%BE.png)
    ![排序状态图](https://git.lug.ustc.edu.cn/luoyanchen/cod/raw/master/Sort/%E6%8E%92%E5%BA%8F%E7%8A%B6%E6%80%81%E5%9B%BE.png)
    ![排序原理](https://git.lug.ustc.edu.cn/luoyanchen/cod/raw/master/Sort/Sort_RTL.png)
    **逻辑设计说明：**
    >&emsp;&emsp;&emsp;&emsp;`x0, x1, x2, x3`为4个4位输入的待排序数据，在`reset`重置信号之后，经过若干个由`clock`输入的时钟周期控制的状态机状态转移，实现类冒泡排序的算法，产生由小到大的有序数列`s0, s1, s2, s3`，并输出`done`信号表示排序结束。状态机有三种状态，分别由独热码表示：无序`State_Disorder = 3'b001`、前两者局部有序`State_01Inorder = 3'b010`、前三者局部有序/全员有序`State_012Inorder = 3'b100`，状态图中没有标出因`reset`信号重置而产生的状态转移。
- **仿真代码 <font face="consolas">Sort_tb.v</font>**
    ```verilog {.line-numbers}
    module Sort_tb();
        reg reset;
        wire clock;
        reg [3 : 0] x0, x1, x2, x3;
        wire [3 : 0] s0, s1, s2, s3;
        wire done;

        GenerateClock CLK (clock);
        Sort DUT (.x0(x0), .x1(x1), .x2(x2), .x3(x3), .reset(reset), .clock(clock), .s0(s0), .s1(s1), .s2(s2), .s3(s3), .done(done));
        
        initial
        begin
            x0 = 4'b1000;
            x1 = 4'b0101;
            x2 = 4'b0001;
            x3 = 4'b0101;
            reset = 'b1;#5;
            reset = 'b0;#195;
            x0 = 4'b0011;
            x1 = 4'b0111;
            x2 = 4'b0000;
            x3 = 4'b0100;
            reset = 'b1;#5;
            reset = 'b0;
        end
        
    endmodule
    ```
- **仿真截图**
    ![排序仿真截图](https://git.lug.ustc.edu.cn/luoyanchen/cod/raw/master/Sort/Sort_simulation.png)
    **仿真截图说明：**
    >对于两组不同的数据输入进行时长400ns的仿真：  
    >- `x0 = 'd8, x1 = 'd5, x2 = 'd1, x3 = 'd5`，依次有以下交换：
    >   | 交换 | s0 | s1 | s2 | s3 |
    >   | :----: | :----: | :----: | :----: | :----: |
    >   |        | 8 | 5 | 1 | 5 |
    >   | s0, s1 | 5 | 8 | 1 | 5 |
    >   | s1, s2 | 5 | 1 | 8 | 5 |
    >   | s0, s1 | 1 | 5 | 8 | 5 |
    >   | s2, s3 | 1 | 5 | 5 | 8 |
    >- `x0 = 'd3, x1 = 'd7, x2 = 'd0, x3 = 'd4`，依次有以下交换：
    >   | 交换 | s0 | s1 | s2 | s3 |
    >   | :----: | :----: | :----: | :----: | :----: |
    >   |        | 3 | 7 | 0 | 4 |
    >   | s1, s2 | 3 | 0 | 7 | 4 |
    >   | s0, s1 | 0 | 3 | 7 | 4 |
    >   | s2, s3 | 0 | 3 | 4 | 7 |
- **下载照片**
    ![排序下载照片1](https://git.lug.ustc.edu.cn/luoyanchen/cod/raw/master/Sort/IMG_20190329_002040.jpg)
    ![排序下载照片2](https://git.lug.ustc.edu.cn/luoyanchen/cod/raw/master/Sort/IMG_20190329_002123.jpg)
    ![排序下载照片3](https://git.lug.ustc.edu.cn/luoyanchen/cod/raw/master/Sort/IMG_20190329_002150.jpg)
    **下载照片说明：**
    >&emsp;&emsp;&emsp;&emsp;将`swt[15], swt[12], swt[10 : 8], swt[5], swt[3], swt[1]`拨至开，按动按钮`BTNU`输入一个复位`reset`电平脉冲后,相应`led[15], led[12], led[10 : 8], led[5], led[3], led[1]`亮起，如图一所示，表示数据输入为`x0 = 4'b1001, x1 = 4'b0111, x2 = 4'b0010, x3 = 4'b1010`  
    >&emsp;&emsp;&emsp;&emsp;按动按钮`BTNC`输入时钟`clock`脉冲信号，经两个周期的交换后，`led[15], led[12 : 11], led[9], led[6 : 4], led[1]`亮起，如图二所示，表示此时排序中间数据为`s0 = 4'b1001, s1 = 4'b1010, s2 = 4'b0111, x3 = 4'b0010`  
    >&emsp;&emsp;&emsp;&emsp;再次按动三次`BTNC`输入三个时钟周期脉冲后，`led[15], led[13], led[11], led[8], led[6 : 4], led[1]`与`led16_G`亮起，如图三所示，表示输出`done = 'b1`，排序已结束，结果为`s0 = 4'b1010, s1 = 4'b1001, s2 = 4'b0111, x3 = 4'b0010`
</br>
### 2. 除法器
- **程序代码 <font face="concolas">DIV.v</font>**
    ```verilog {.line-numbers}
    module DIV (clock, reset, x, y, q, r, error, done);
        parameter N = 4;
        parameter State_Begin = 3'b001,
                State_Shift = 3'b010,
                State_Finish = 3'b100;
        input clock;
        input reset;
        input [N - 1 : 0] x;
        input [N - 1 : 0] y;
        output wire [N - 1 : 0] q;
        output wire [N - 1 : 0] r;
        output reg error, done;
        reg [2 : 0] currentstate, nextstate;
        reg [N - 1 : 0] count;
        reg [2 * N - 1 : 0] Data;

        always @(posedge clock or posedge reset)
        begin
            if (reset)
                currentstate <= State_Begin;
            else
                currentstate <= nextstate;
        end
        
        always @(*)
        begin
            case (currentstate)
                State_Begin:
                begin
                    if (y == 0)
                        nextstate = State_Begin;
                    else
                    begin
                        if (count == N)
                            nextstate = State_Shift;
                        else
                            nextstate = State_Begin;
                    end
                end
                
                State_Shift:
                begin				
                    if (count < 'd2)
                        nextstate = State_Finish;
                    else
                        nextstate = State_Shift;
                end
                
                State_Finish:
                    nextstate = State_Finish;

                default :
                    nextstate = State_Begin;
            endcase 
        end
        
        always @(posedge clock or posedge reset)
        begin
            if (reset)
            begin
                count <= N;
                done <= 1'b0;
                error <= 1'b0;
            end
            else
            begin
                case (currentstate)
                    State_Begin:
                    begin
                        if (y == 0)
                        begin
                            error <= 1'b1;
                            Data <= 0;
                        end
                        else
                        begin
                            if (count == N)
                                Data <= {{N{1'b0}}, x};
                        end
                    end
                    
                    State_Shift:
                    begin
                        count <= count - 3'b001;				
                        if (Data[(2 * N - 2) : N - 1] >= y)
                            Data <= (Data << 1) + 1'b1 - (y << N);
                        else
                            Data <= (Data << 1);
                    end
                    
                    State_Finish:
                        done <= 1'b1;
                        
                    default :
                    begin
                        done <= 1'b0;
                        error <= 1'b0;
                    end
                endcase
            end 
        end
        
        assign q = Data[N - 1 : 0];
        assign r = Data[2 * N - 1 : N];

    endmodule
    ```
- **原理逻辑电路图**
    ![除法器逻辑](https://git.lug.ustc.edu.cn/luoyanchen/cod/raw/master/DIV/%E9%99%A4%E6%B3%95%E5%99%A8%E9%80%BB%E8%BE%91%E5%9B%BE.png)
    ![除法器状态图](https://git.lug.ustc.edu.cn/luoyanchen/cod/raw/master/DIV/%E9%99%A4%E6%B3%95%E5%99%A8%E7%8A%B6%E6%80%81%E5%9B%BE.png)
    ![除法器原理](https://git.lug.ustc.edu.cn/luoyanchen/cod/raw/master/DIV/DIV_RTL.png)
    **逻辑设计说明：**
    >&emsp;&emsp;&emsp;&emsp;`x, y`为2个4位输入的数据，分别表示被除数与除数，在`reset`重置信号之后，利用二进制数的扩展与移位实现整数的整除，中间数据`Data`是由被除数`x`位数加倍扩展之后的8位数据，每个移位状态下的时钟周期间，`Data`向左移位一位，并检查前4位与除数`y`的大小，若大于则减去之，并在`Data`末位置`'d1`，经过4个由`clock`输入的时钟周期控制的状态机状态转移，由`Data`的后4位与前4位产生商和余数`q, r`，并输出`done`信号表示排序结束；若输入的`y`为`0`，则能输出`error`信号表示错误。状态机有三种状态，分别由独热码表示：准备`State_Begin = 3'b001`，移位`State_Shift = 3'b010`，结束`State_Finish = 3‘b100`，状态图中没有标出因`reset`信号重置而产生的状态转移。
- **仿真代码 <font face="consolas">DIV_tb.v</font>**
    ```verilog {.line-numbers}
    module DIV_tb();
        wire clock, error, done;
        wire [3 : 0] q, r;
        reg reset;
        reg [3 : 0] x, y;

        GenerateClock C (clock);
        DIV DUT (.clock(clock), .reset(reset), .x(x), .y(y), .q(q), .r(r), .error(error), .done(done));//, .currentstate(currentstate), .nextstate(nextstate), .count(count) ,.Data(Data));

        initial 
        begin
            x = 4'b1111;
            y = 4'b0010;
            reset = 'b1;#10;
            reset = 'b0;#110;
            x = 4'b0111;
            y = 4'b0011;
            reset = 'b1;#10;
            reset = 'b0;#110;
            x = 4'b1010;
            y = 4'b0000;
            reset = 'b1;#10;
            reset = 'b0;
        end
        
    endmodule
    ```
- **仿真截图**
    ![除法器仿真截图](https://git.lug.ustc.edu.cn/luoyanchen/cod/raw/master/DIV/DIV_simulation.png)
    **仿真截图说明：**
    >对于三组不同的数据输入进行时长280ns的仿真：  
    >- `x = 4'b1111 = 'd15, y = 4'b0010 = 'd2`，`Data`发生的移位过程如下
    >   | Data | 7 | 6 | 5 | 4 | 3 | 2 | 1 | 0 |
    >   | :----: | :----: | :----: | :----: | :----: | :----: | :----: | :----: | :----: |
    >   | 1 | 0 | 0 | 0 | 0 | 1 | 1 | 1 | 1 |
    >   | 2 | 0 | 0 | 0 | 1 | 1 | 1 | 1 | 0 |
    >   | 3 | 0 | 0 | 0 | 1 | 1 | 1 | 0 | 1 |
    >   | 4 | 0 | 0 | 0 | 1 | 1 | 0 | 1 | 1 |
    >   | 5 | 0 | 0 | 0 | 1 | 0 | 1 | 1 | 1 |
    >   得到结果商`q = 4'b0111 = 'd7, r = 4'b0001 ='d1`
    >- `x = 4'b0111 = 'd7, y = 4'b0011 = 'd3`，`Data`发生的移位过程如下
    >   | Data | 7 | 6 | 5 | 4 | 3 | 2 | 1 | 0 |
    >   | :----: | :----: | :----: | :----: | :----: | :----: | :----: | :----: | :----: |
    >   | 1 | 0 | 0 | 0 | 0 | 0 | 1 | 1 | 1 |
    >   | 2 | 0 | 0 | 0 | 0 | 1 | 1 | 1 | 0 |
    >   | 3 | 0 | 0 | 0 | 1 | 1 | 1 | 0 | 0 |
    >   | 4 | 0 | 0 | 0 | 0 | 1 | 0 | 0 | 1 |
    >   | 5 | 0 | 0 | 0 | 1 | 0 | 0 | 1 | 0 |
    >   得到结果商`q = 4'b0010 = 'd2, r = 4'b0001 ='d1`
    >- `x = 4'b1010 = 'd10, y = 4'b0000 = 'd0`，运算错误`error = 'b1`
- **下载照片**
    ![除法器下载照片1](https://git.lug.ustc.edu.cn/luoyanchen/cod/raw/master/DIV/IMG_20190328_235512.jpg)
    ![除法器下载照片2](https://git.lug.ustc.edu.cn/luoyanchen/cod/raw/master/DIV/IMG_20190328_235542.jpg)
    **下载照片说明：**
    >&emsp;&emsp;&emsp;&emsp;将`swt[7], swt[5], swt[1 : 0]`拨至开，表示输入为`x = 4'b1010, y = 4'b0011`，拨动`swt[14]`输入重置信号，并拨动`swt[15]`输入时钟脉冲，如图二所示状态；拨动`swt[15]`4次后，`led[12], led[1 : 0]`与`led16_G`亮起，表示运算结束，结果商`q = 4'b0011`，余数`r = 4'b0001`
</br> 
## 实验总结
<p>&emsp;&emsp;&emsp;&emsp;本次实验主要聚焦于复习有限状态机FSM相关知识、三段式状态机实现以及其在实际用例中的实现，通过实现排序器与除法器，根据实际情况分析状态并绘制状态图并加以实现的能力得到了锻炼，并对数字电路中的控制单元与数据通路的特点与联系有了更深入的了解。另一方面，通过对状态机在硬件上的表现进行观察，对于二段式在时钟边沿的毛刺，以及三段式状态机对于硬件资源的利用有了一定的认识，在日后的电路设计上提供了e宝贵的经验与方向。</p>  

## 附
&emsp;&emsp;&emsp;&emsp;本报告中出现的所有设计与测试源代码文件以及相关截图与照片可见  
&emsp;&emsp;&emsp;&emsp;[GitLab_COD@luoyanchen](https://git.lug.ustc.edu.cn/luoyanchen/cod)