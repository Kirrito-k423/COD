# <center>实验报告</center>  
### <center>实验四	存储器与显示控制器</center>  
##### <p align="right">罗晏宸</br>PB17000297</br>2019.4.18</p>  
***  
## 实验目的  
1. **学习RAM相关ip核例化方法**  
    &emsp;&emsp;学习Distributed Memory Generator与Block Memory Generator两个ip核的功能异同、作用特性与例化时的选项与要点；学习`.coe`文件的编写与加载；理解RAM模块在整体工程中的作用  
2. **学习VGA信号的产生与显示**  
    &emsp;&emsp;学习VGA显示的原理，体会行同步信号与场同步信号以及定时参数在VGA扫描中的作用  
3. **利用RAM实现基于VGA的画板**  
    &emsp;&emsp;利用已例化的RAMip核，通过编写PCU（Paint Control Unit，绘画控制单元）、DCU（Display Control Unit，显示控制单元）实现一个在`800x600@72Hz`分辨率下的、可设置画笔RGB颜色的`256x256`像素画板  
***  
## 实验内容  
### 1. VGA画板  
- **程序代码 <font face="consolas">Brush.v</font>**  
    ```verilog {.line-numbers}
    module Brush(Brush_Colour, BTNU, BTNL, BTNR, BTND, draw, CLK100MHZ, reset, VGA_R, VGA_G, VGA_B, VGA_HS, VGA_VS);
        input [11 : 0] Brush_Colour;
        input BTNU, BTNL, BTNR, BTND;
        input draw, CLK100MHZ, reset;
        output [3 : 0] VGA_R, VGA_G, VGA_B;
        output VGA_HS, VGA_VS;
        
        wire CLK50MHZ, CLK10HZ;
        wire [9 : 0] Point_X, Point_Y;
        wire DisplayEnable;
        reg [11 : 0] RGB;
        reg [7 : 0] Cursor_X, Cursor_Y;
        wire [11 : 0] Board_Colour;
        wire IfCursor, IfSquare;
        wire [7 : 0] X, Y;
        wire [7 : 0] Write_X, Write_Y;
        wire [11 : 0] Write_Colour;
        wire Write_Clock;
        
        //---------VGA signal------------
        //800x600@72Hz
        parameter H_PERIOD = 1040, 
                    HSPW = 120, 
                    HBP = 64, 
                    H_Length = 800, 
                    HFP = 56; 
        parameter V_PERIOD = 666, 
                    VSPW = 6, 
                    VBP = 23, 
                    V_Length = 600, 
                    VFP = 37;
                    
        clk_wiz_0 CLK100MHZ_to_CLK50MHZ (CLK50MHZ, CLK100MHZ);
        Counter H_COUNT (CLK50MHZ, 0, H_PERIOD, Point_X);
        Counter V_COUNT (~(Point_X[9]), 0, V_PERIOD, Point_Y);

        // SPW|BP|Length|FP
        assign VGA_HS = ~(Point_X < HSPW);
        assign VGA_VS = ~(Point_Y < VSPW);
        assign DisplayEnable = 	   (Point_Y >= (VSPW + VBP)) 
                                && (Point_Y < (V_PERIOD - VFP)) 
                                && (Point_X >= (HSPW + HBP)) 
                                && (Point_X < (H_PERIOD - HFP));
        assign VGA_R = DisplayEnable ? RGB[11:8] : 0;
        assign VGA_G = DisplayEnable ? RGB[7:4] : 0;
        assign VGA_B = DisplayEnable ? RGB[3:0] : 0;
        //-------------------------------

        //------Cursor movement---------
        assign IfCursor = 	   ((X == Cursor_X) 
                    && (Y < Cursor_Y + 'd4) 
                    && (Y > Cursor_Y - 'd4)) 
                    || ((Y == Cursor_Y) 
                    && (X < Cursor_X + 'd4) 
                    && (X > Cursor_X - 'd4));

        always @(posedge CLK10HZ or posedge reset)
        begin
            if (reset)
            begin
                Cursor_X <= 'd128;
                Cursor_Y <= 'd128;
            end
            else
            begin
                case ({BTNL, BTNR})
                    2'b10: Cursor_X <= Cursor_X - 'b1;
                    2'b01: Cursor_X <= Cursor_X + 'b1;
                    default: Cursor_X <= Cursor_X;
                endcase
                case ({BTNU, BTND})
                    2'b10: Cursor_Y <= Cursor_Y - 'b1;
                    2'b01: Cursor_Y <= Cursor_Y + 'b1;
                    default: Cursor_Y <= Cursor_Y;
                endcase
            end
        end
        //-------------------------------

        //--------Colour set-------------
        FrequencyDivision CLK50MHZ_to_CLK10HZ (CLK50MHZ, 'b0, CLK10HZ); 
        dist_mem_gen_0 VRAM ({Write_X, Write_Y}, Write_Colour, 
                            {X, Y}, Write_Clock, 
                            draw, Board_Colour);
                            
        assign IfSquare = 	   (('d272 + HSPW + HBP) <= Point_X) 
                            && (Point_X < ('d528 + HSPW + HBP)) 
                            && (('d172 + VSPW + VBP) < Point_Y) 
                            && (Point_Y < ('d428 + VSPW + VBP));
        assign X = IfSquare ? Point_X - ('d272 + HSPW + HBP) : 'b0;
        assign Y = IfSquare ? Point_Y - ('d172 + VSPW + VBP) : 'b0;
        assign Write_X = reset ? X : Cursor_X;
        assign Write_Y = reset ? Y : Cursor_Y;
        assign Write_Colour = reset ? 12'hfff : Brush_Colour;
        assign Write_Clock = reset ? CLK50MHZ : CLK10HZ;
        
        always @(Point_X or Point_Y)
        begin
            if (reset)
            begin
                RGB <= 12'h000;
            end
            else
            begin
                case ({IfSquare, IfCursor})
                    2'b00: RGB <= 12'h000;
                    2'b10: RGB <= Board_Colour;
                    2'b01: RGB <= 12'hFFF;
                    2'b11: RGB <= 12'hFFF - Board_Colour;
                endcase 
            end
        end
        //-------------------------------
        
    endmodule
    ```  
    ```verilog {.line-numbers}
    module Counter(
        input clock,             
        input reset,             
        input [15:0] Range,     
        output reg [15:0] Counter 
    );

        always@(posedge clock or posedge reset) 
        begin
            if (reset) 
                Counter <= 0;
            else 
                if(Counter == Range - 1) 
                    Counter <= 0;
                else 
                    Counter <= Counter + 1;
        end

    endmodule
    ```  
    ```verilog {.line-nnumbers}
    module FrequencyDivision(input Origin_Clock, input reset, output pulse);
        reg [23:0] count;
        always @(posedge Origin_Clock or posedge reset)
        begin
            if (reset)
                count <= 'd0;  		
            else 
            begin
                if(count >= 'd499999)
                    count	<= 'd0;
                else
                    count	<= count + 'd1;
            end
        end
        assign	pulse =  (count == 'd299999) ? 1'b1 : 1'b0;
    endmodule
    ```  
- **原理逻辑电路图**  
    ![VGA画板原理](https://github.com/lyc0930/COD/blob/master/VGA_Brush/VGA_Brush_RTL.png?raw=true)  
    ![VRAM复位逻辑](https://github.com/lyc0930/COD/blob/master/VGA_Brush/%E5%A4%8D%E4%BD%8D%E9%80%BB%E8%BE%91.png?raw=true)  
    **逻辑设计说明：**  
    >&emsp;&emsp;&emsp;&emsp;本工程通过例化深度为`65536`，数据位宽为`12`的Distributed Memory Generator IP核，并使用VGA信号输出`VGA_HS, VGA_VS, VGA_RGB[12 : 0]`实现了一个相对实用的基于分辨率`800x600@72Hz`下VGA显示的画板（此分辨率的定时参数如代码中`parameter`参数表所列），主模块通过读入板载`CLK100MHZ`时钟以及用户输入的画笔颜色`Brush_Colour[12 : 0]`、控制光标连续移动的按钮动作`BTNU, BTND, BTNL, BTNR`、绘画使能`draw`与复位信号`reset`，将用户意图绘制并设定颜色的像素线条显示在屏幕上`256x256`像素范围的“画板”内，或是清空画板并将光标复位居中（复位信号）  
    >&emsp;&emsp;&emsp;&emsp;例化的VRAM模块目的较为单纯————实现将用户在`256x256`画板上所绘制的线条数据存储下来，VRAM默认数据为`12'hFFF`白色。主模块中DCU部分控制VGA信号的方式是普遍的，如代码所示，由两个设定范围的由`50MHz`时钟驱动计数器模块控制高度与场帧扫描信号，并由此进一步得到相应消隐信号的输出与颜色选取（画板外为`12'h000`黑色，画板内各像素为VRAM中存储的对应颜色，在十字光标处通过反色进行高对比度区分），不再进一步赘述了。  
    >&emsp;&emsp;&emsp;&emsp;下面对工程实现的复位功能做一些说明，如上图一所示，整个工程的总线图较为庞大复杂，图二是省略一些线网与接口之后重绘的复位功能实现逻辑电路图。当复位按钮按下，`reset`信号为高电平时，光标坐标`{Cursor_X, Cursor_Y}`重置为画板中心，并且由多个与`reset`相连的多路选择器控制，输入VRAM的写数据信号由：    
    >- “由`draw`信号使能，在10Hz时钟（与光标移动同频）上升沿向光标所在坐标对应的寄存器地址`{Cursor_X, Cursor_Y}`写入画笔颜色数据`Brush_Colour[12 : 0]`”  
    >
    >变为：  
    >- “使能始终为高电平，在50MHz时钟（与VGA扫描信号同频）上升沿向VGA扫描信号所在坐标对应的寄存器地址`{Point_X, Point_Y}`写入`12'hFFF`白色”  
    >
    >通过以高频时钟向VRAM内写入数据，将其数据全部重置为`12'hFFF`，实现将画板上颜色“擦除”的效果  
- **下载照片**
    ![排序下载照片1](https://github.com/lyc0930/COD/blob/master/VGA_Brush/IMG_20190418_191037.jpg?raw=true)
    **下载照片说明：**
    >&emsp;&emsp;&emsp;&emsp;如图所示，在分辨率`800x600@72Hz`的显示屏中央有一块`256x256`像素的以白色为底色的区域，经过用户一定的绘制，其上有不同颜色的水平、竖直以及沿对角方向的线条，当前光标位置由一个与背景反色的十字光标标志。  
</br>
## 实验总结
<p>&emsp;&emsp;本次实验主要内容是学习VGA显示信号的原理，在此前数字电路实验课程期末综合设计中，我曾对VGA显示有过一定的尝试，但对其原理及实现并不甚了解，经过本次实验的学习，我对于不同分辨率下高度与场帧扫描信号、消隐信号的作用与数值有了相对深入与系统的认识，能够独立编写出一定分辨率下适配性较好的VGA显示控制模块。本次实验的另一部分是对于RAM相关IP核的学习，在Vivado2018.3开发环境下，Distributed Memory Generator与Block Memory Generator两者的区别主要体现在后者更适用于数据量较大的情形，对于本次实验65536的深度与12的位宽而言，两者的使用差别并不明显；例化IP核时加载的coe文件为其提供了定制的初始值，相应的会降低例化的效率与成功率，但所学的coe文件的编写方法为之后的有关实验提供了更多功能的实现途径</p>  

## 附
&emsp;&emsp;本报告中出现的所有设计与测试源代码文件以及相关截图与照片可见  
&emsp;&emsp;[GitHub@lyc0930](https://github.com/lyc0930/COD)